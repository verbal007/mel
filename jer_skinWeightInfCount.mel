///////////////////////////////////////////////////////////////////////////
/* jer_skinWeightInfCount

Created by Jeremy Sabo on Jan 1, 2008

Desc:	Select skinned verticies and run script.  The vertices that remain
		selected have more than 3 influences.

		Special thanks to Michael Comet for :
			mcomets_getSkinFromGeo & mcomets_getJointsFromComps
			& inspiring this here wannabe animator.  :)

*/
///////////////////////////////////////////////////////////////////////////


global proc string[] mcomets_getSkinFromGeo(string $geo)
{
    string $skins[] ;
    clear $skins ;

    if ($geo == "" || objExists($geo) != true)
    	return $skins ;

    string $hist[] = `listHistory -pdo 1 -il 2 $geo` ;
    string $h ;
    
    for ($h in $hist)
    	{
    	if (nodeType($h) == "skinCluster")
    	    {
    	    $skins[size($skins)] = $h ;	// Store it
    	    }
    	}
    	
    return $skins ;
}

proc string[] mcomets_getJointsFromComps(string $skin, string $comps[])
{
    string $jnts[] ;
    clear $jnts ;
    

         // Get all joints used in skin cluster...
    string $skinJnts[] = `skinCluster -q -influence $skin` ;
    
    string $c;
    for ($c in $comps)
        {
           // Get weights for this component...
        float $wts[] = `skinPercent -q -v $skin $c` ;

	   // Now for each non zero weight...
	int $w ;
	for ($w=0 ; $w < size($wts); ++$w)
	    {
	    if ($wts[$w] > 0.0)
	        {
		// If it is non zero, then add the joint with this index to the list
		// if it hasn't been added yet.
		int $idx = -1 ;
		int $j ;
		for ($j=0; $j < size($jnts); ++$j)
		    {
		    if ($jnts[$j] == $skinJnts[$w])
		        {
			$idx = $j ;
			break ;
			}
		    }
		if ($idx == -1)  // If still -1, then we haven't seen it yet!
		    {
		    $jnts[size($jnts)] = $skinJnts[$w] ;
		    }
		    
		}  // end of if wt > 0

	    } // end of each wt
	    
    	} // end of sel


    return $jnts ;

}

global proc jer_skinWeightInfCount(int $infCount){

string $each;
string $skins[];
string $infList[];
string $sel[] = `ls -sl -flatten`;
string $components[];

select -clear;

for ($each in $sel){
	// convert selected component to array for compatibility w/ cometSaveWeights
	$components[0] = $each;
	$skins = `mcomets_getSkinFromGeo $components[0]`;
	// get influence list from selected components
	$infList = `mcomets_getJointsFromComps $skins[0] $components`;
	if (`size $infList` > $infCount){
        select -add $components;
	}
}

} // END // global proc jer_skinWeightInfCount(){